////////////////////////////////////////////////////////////////////////
// Модуль поддерживает работу с хранилищем общих настроек

// Получение каталога, используемого для расположения локальных файлов
// Возвращаемое значение: 
//  Строка, содержащая каталог
Функция ПолучитьРабочийКаталог() Экспорт

	Возврат ХранилищеОбщихНастроек.Загрузить("РабочийКаталогПользователя");

КонецФункции

// Сохранение каталога, используемого для расположения локальных файлов
// Параметры: 
//  Строка, содержащая каталог
Процедура СохранитьРабочийКаталог(Каталог) Экспорт
	
	ХранилищеОбщихНастроек.Сохранить("РабочийКаталогПользователя",,Каталог);	

КонецПроцедуры

// Процедура получает список файлов, которые переданы на сервер и создает соответствующие элементы справочника
Процедура СоздатьЭлементыСправочника(СписокЗагруженныхФайлов, Владелец) Экспорт
	
	Для каждого ЗагруженныйФайл Из СписокЗагруженныхФайлов Цикл
		
		Файл = Новый Файл(ЗагруженныйФайл.Значение.Имя);
		ХранимыйФайл = Справочники.ХранимыеФайлы.СоздатьЭлемент();
		ХранимыйФайл.ОбъектДанных = Владелец;
		ХранимыйФайл.Наименование = Файл.Имя;
		ХранимыйФайл.ИмяФайла = Файл.Имя;
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(ЗагруженныйФайл.Значение.Хранение);
		ХранимыйФайл.ДанныеФайла = Новый ХранилищеЗначения(ДвоичныеДанные, Новый СжатиеДанных());
		ХранимыйФайл.Записать();
		
	КонецЦикла;
	
КонецПроцедуры
