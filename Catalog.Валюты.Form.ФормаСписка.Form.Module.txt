&НаСервере
// Обработчик события ПриСоздании формы.
// Устанавливает параметр КонецПериода запроса равным текущей дате.
//
Процедура ПриСоздании(Отказ, СтандартнаяОбработка)
	
	ДатаКурса = НачалоДня(ТекущаяДата());
	Элементы.Курс.Заголовок = "Курс на " + Формат(ТекущаяДата(), "ДЛФ=DD");
	Список.Параметры.УстановитьЗначениеПараметра ("КонецПериода", ДатаКурса);
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "обработка выбора" формы
// Обработка выбора валюты при подборе из ОКВ
//
Процедура ОбработкаВыбора(РезультатВыбора, ИсточникВыбора)
	
	Если ТипЗнч(РезультатВыбора) = Тип("Структура") Тогда
		ОткрытьФорму("Справочник.Валюты.ФормаОбъекта", РезультатВыбора,ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Обработчик нажатия кнопки "загрузить курсы".
// Получает и открывает форму загрузки курсов.
//
Процедура КомандаЗагрузитьКурсыВыполнить()
	
	#Если Клиент Или ТонкийКлиент Тогда
	ОткрытьФорму("Обработка.КурсыВалютРБК.Форма.Форма");
	#КонецЕсли
	
	#Если ВебКлиент Тогда
	Предупреждение(НСтр("ru = 'В веб-клиенте загрузка курсов валют не поддерживается'"));
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
// Обработчик нажатия кнопки "Подбор из ОКВ".
// Получает и открывает форму подбора валюты из классификатора ОКВ.
//
Процедура КомандаПодборИзОКВВыполнить()
	
	ОткрытьФорму("Справочник.Валюты.Форма.ФормаПодбораВалютИзОКВ",, ЭтаФорма, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "обработка оповещения" формы.
// Пересчитывает список валют.
//
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзменениеКурсовВалют" Тогда
		Элементы.Валюты.Обновить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗагрузитьКурсыСНБУ(Команда)
	
	#Если Клиент Или ТонкийКлиент Тогда
	ОткрытьФорму("Обработка.КурсыВалютНБУ.Форма.Форма");
	#КонецЕсли
	
	#Если ВебКлиент Тогда
	Предупреждение(НСтр("ru = 'В веб-клиенте загрузка курсов валют не поддерживается'"));
	#КонецЕсли

КонецПроцедуры
