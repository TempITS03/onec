
&НаСервере
// Обработчик события "при создании на сервере"
// Вызывает функциональность заполнения списка валют из ОКВ
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗакрыватьПриВыборе = Ложь;
	ЗаполнитьТаблицуВалют();
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "выбор" элемента формы СписокВалют
// Оповещает о выборе вызывающую форму. Форма закрывается.
//
Процедура СписокВалютВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОбработатьВыборВСпискеВалют(Элемент.ТекущиеДанные);
	
КонецПроцедуры

&НаСервере
// Заполняет список валют из макета ОКВ
//
Процедура ЗаполнитьТаблицуВалют()
	
	КлассификаторXML = Справочники.Валюты.ПолучитьМакет("ОбщероссийскийКлассификаторВалют").ПолучитьТекст();
	
	КлассификаторТаблица = ОбщегоНазначения.ПрочитатьXMLВТаблицу(КлассификаторXML).Данные;
	
	Для Каждого ЗаписьОКВ Из КлассификаторТаблица Цикл
		НоваяСтрока = Валюты.Добавить();
		НоваяСтрока.КодВалютыЦифровой  = ЗаписьОКВ.Code;
		НоваяСтрока.КодВалютыБуквенный = ЗаписьОКВ.CodeSymbol;
		НоваяСтрока.Наименование       = ЗаписьОКВ.Name;
		НоваяСтрока.СтраныИТерритории  = ЗаписьОКВ.Description;
		НоваяСтрока.Загружается        = ЗаписьОКВ.RBCLoading;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
// Обработчик нажатия на кнопку "Выбрать" командной панели
// элемента формы СписокВалют.
//
Процедура ВыбратьВыполнить()
	
	ОбработатьВыборВСпискеВалют(Элементы.СписокВалют.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
// Обработчик события "выбор" элемента формы СписокВалют
// Оповещает о выборе вызывающую форму. Форма закрывается.
//
Процедура ОбработатьВыборВСпискеВалют(ТекущиеДанные)
	
	ПараметрыВыбор = Новый Структура("КодВалюты, НаименованиеКраткое, НаименованиеПолное, Загружается");
	ПараметрыВыбор.КодВалюты           = ТекущиеДанные.КодВалютыЦифровой;
	ПараметрыВыбор.НаименованиеКраткое = ТекущиеДанные.КодВалютыБуквенный;
	ПараметрыВыбор.НаименованиеПолное  = ТекущиеДанные.Наименование;
	ПараметрыВыбор.Загружается         = ТекущиеДанные.Загружается;
	
	ОповеститьОВыборе(ПараметрыВыбор);
	
КонецПроцедуры

