// Переменные, используемые в процедурах контроля остатков
Перем МетаданныеДокумента, ИмяДокумента, ИмяТабличнойЧасти, ИмяТаблицы, СтруктураШапкиДокумента, Отказ;
Перем ДокументСсылка;


// Процедура контролирует остаток по данному регистру по переданному документу
// и его табличной части. В случае недостатка товаров выставляется флаг отказа и 
// выдается сообщение.
//
// Параметры:
//  ДокументОбъект     - объект проводимого документа, 
//  ИмяТабЧасти        - строка, имя табличной части, которая проводится по регистру, 
//  СтруктураШапки     - структура, содержащая значения "через точку" ссылочных реквизитов по шапке документа,
//  ОтказПроведения    - флаг отказа в проведении,
//  ЗаголовокСообщения - строка, заголовок сообщения об ошибке проведения.
//
Процедура КонтрольОстатков(ДокументОбъект, ИмяТабЧасти, СтруктураШапки, ОтказПроведения) Экспорт

	Если ИмяТабЧасти <> "" Тогда  //Имя таб части может быть не указана для документа КомплектацияНоменклатуры с видом операции Разукомплектация
		Если ДокументОбъект[ИмяТабЧасти].Количество()=0 Тогда //если в таб.части ничего нет - не надо проверять
			Возврат;
		КонецЕсли;
	КонецЕсли;

	//Если УправлениеДопПравамиПользователей.РазрешеноПревышениеОстаткаТоваровНаСкладе() Тогда
	//	Возврат;
	//КонецЕсли;

	СтруктураШапкиДокумента = СтруктураШапки;
	ИмяТабличнойЧасти       = ИмяТабЧасти;
	Отказ                   = ОтказПроведения;

	
	ДокументСсылка		= ДокументОбъект.Ссылка;
	МетаданныеДокумента = ДокументОбъект.Метаданные();
	ИмяДокумента        = МетаданныеДокумента.Имя;

	Если ИмяТабЧасти <>"" Тогда
		ИмяТаблицы = ИмяДокумента + "." + СокрЛП(ИмяТабличнойЧасти);
	Иначе
		ИмяТаблицы = ИмяДокумента;
	КонецЕсли;

	Если ИмяДокумента = "ТабельРабот" Тогда
		КонтрольОстатков_ТабельРабот();
	Иначе
		 Сообщить("Для документа "+ДокументОбъект+" не предусмотрен вызов процедуры 'Контроль остатков' модуля регистра 'Товары в рознице'");
	КонецЕсли;
	
	//вернем обратно признак отказа от проведения документа 
	ОтказПроведения = Отказ;

КонецПроцедуры // КонтрольОстатков()

Процедура КонтрольОстатков_ТабельРабот() 
	
	Запрос = Новый Запрос();
	
	Текст = "ВЫБРАТЬ
	|	Табель.ДокументРасчета,
	|	СУММА(РасчетПоЗадачамОстатки.КвоЧасовОстаток) КАК КвоЧасовОстаток,
	|	СУММА(Табель.КоличествоЧасовЗатрачено) КАК КвоЧасовЗатраченоПоДокументу,
	|	ЕСТЬNULL(РасчетПоЗадачамОстатки.КвоЧасовОстаток, 0) - ЕСТЬNULL(Табель.КоличествоЧасовЗатрачено, 0) КАК Разница
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТабельРаботУслуги.ДокументРасчета КАК ДокументРасчета,
	|		СУММА(ТабельРаботУслуги.КоличествоЧасовЗатрачено) КАК КоличествоЧасовЗатрачено
	|	ИЗ
	|		Документ.ТабельРабот.Услуги КАК ТабельРаботУслуги
	|	ГДЕ
	|		ТабельРаботУслуги.Ссылка = &Ссылка
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТабельРаботУслуги.ДокументРасчета) КАК Табель
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетПоЗадачам.Остатки(
	|				,
	|				Контрагент = &Контрагент
	|					И Организация = &Организация
	|					И ДокументРасчета В (&ДокументРасчета)) КАК РасчетПоЗадачамОстатки
	|		ПО Табель.ДокументРасчета = РасчетПоЗадачамОстатки.ДокументРасчета
	|
	|СГРУППИРОВАТЬ ПО
	|	Табель.ДокументРасчета,
	|	ЕСТЬNULL(РасчетПоЗадачамОстатки.КвоЧасовОстаток, 0) - ЕСТЬNULL(Табель.КоличествоЧасовЗатрачено, 0)
	|
	|ИМЕЮЩИЕ
	|	СУММА(ЕСТЬNULL(РасчетПоЗадачамОстатки.КвоЧасовОстаток, 0) - ЕСТЬNULL(Табель.КоличествоЧасовЗатрачено, 0)) < 0
	|
	|ДЛЯ ИЗМЕНЕНИЯ
	|	РегистрНакопления.РасчетПоЗадачам.Остатки";
	
	Запрос.Текст = Текст;
	
	МассивДокументРасчета = ДокументСсылка.Услуги.ВыгрузитьКолонку("ДокументРасчета");
	
	Запрос.УстановитьПараметр("Контрагент",СтруктураШапкиДокумента.Контрагент);
	Запрос.УстановитьПараметр("Организация",СтруктураШапкиДокумента.Организация);
	Запрос.УстановитьПараметр("Ссылка",ДокументСсылка);
	Запрос.УстановитьПараметр("ДокументРасчета",МассивДокументРасчета);
	
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл

		Отказ = Истина;
		
		Сообщить("По: " + СокрЛП(Выборка.ДокументРасчета) + " количество часов декларированно программистом: " + Выборка.КвоЧасовОстаток + Символы.ПС
						+ "часов указано в табеле: " + Выборка.КвоЧасовЗатраченоПоДокументу + Символы.ПС 
						+ "разница недостачи: " + Выборка.Разница,СтатусСообщения.Важное);

						
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "Документ не проведен: " + Строка(ДокументСсылка);
		Сообщение.КлючДанных = Выборка.ДокументРасчета;
		Сообщение.Сообщить();

	КонецЦикла;

	
КонецПроцедуры	

