
&НаСервере
// Обработчик события "при создании на сервере"
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НастройкаПроксиНаКлиенте = Параметры.НастройкаПроксиНаКлиенте;
	
	Если НастройкаПроксиНаКлиенте Тогда
		НастройкаПроксиСервера = ХранилищеОбщихНастроек.Загрузить("НастройкаПроксиСервера");
	Иначе
		НастройкаПроксиСервера = ПолучениеФайловИзИнтернет.ПолучитьНастройкиПроксиНаСервере1СПредприятие();
	КонецЕсли;
	
	Если ТипЗнч(НастройкаПроксиСервера) = Тип("Соответствие") Тогда
		ИспользоватьПрокси = НастройкаПроксиСервера["ИспользоватьПрокси"];
		Сервер       = НастройкаПроксиСервера["Сервер"];
		Пользователь = НастройкаПроксиСервера["Пользователь"];
		Пароль       = НастройкаПроксиСервера["Пароль"];
		Порт         = НастройкаПроксиСервера["Порт"];
		НеИспользоватьПроксиДляЛокальныхАдресов =
		           НастройкаПроксиСервера["НеИспользоватьПроксиДляЛокальныхАдресов"];
	КонецЕсли;
	
	Элементы.ПараметрыПрокси.Доступность = ?(ИспользоватьПрокси, Истина, Ложь);
	
КонецПроцедуры

&НаКлиенте
// Обработчик события нажатия на кнопку "ОК" командной панели формы
// Закрывает форму, передавая в качестве возвращаемого результата
// параметры прокси.
//
Процедура ОкВыполнить()
	
	НастройкаПроксиСервера = Новый Соответствие;
	НастройкаПроксиСервера.Вставить("ИспользоватьПрокси", ИспользоватьПрокси);
	НастройкаПроксиСервера.Вставить("Пользователь", Пользователь);
	НастройкаПроксиСервера.Вставить("Пароль", Пароль);
	НастройкаПроксиСервера.Вставить("Порт", Порт);
	НастройкаПроксиСервера.Вставить("Сервер", Сервер);
	НастройкаПроксиСервера.Вставить("НеИспользоватьПроксиДляЛокальныхАдресов",
	                              НеИспользоватьПроксиДляЛокальныхАдресов);
	
	СохранитьНастройкиПроксиСервера(НастройкаПроксиНаКлиенте, НастройкаПроксиСервера);
	
	Закрыть(НастройкаПроксиСервера);
	
КонецПроцедуры

&НаКлиенте
// Изменяем доступность группы редактирования параметров прокси если они не используются
//
Процедура ИспользоватьПроксиПриИзменении(Элемент)
	
	Элементы.ПараметрыПрокси.Доступность = ?(ИспользоватьПрокси, Истина, Ложь);
	
КонецПроцедуры

&НаСервереБезКонтекста
// Сохранение настроек прокси
//
Процедура СохранитьНастройкиПроксиСервера(НастройкаПроксиНаКлиенте, НастройкаПроксиСервера)
	
	Если НастройкаПроксиНаКлиенте Тогда
		ХранилищеОбщихНастроек.Сохранить("НастройкаПроксиСервера", , НастройкаПроксиСервера);
	Иначе
		ПолучениеФайловИзИнтернет.СохранитьНастройкиПроксиНаСервере1СПредприятие(НастройкаПроксиСервера);
	КонецЕсли;
	
КонецПроцедуры

