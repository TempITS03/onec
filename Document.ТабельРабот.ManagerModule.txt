
Процедура ПечатьТабеляКратко(ТабДокумент, Ссылка) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТабельРабот.Дата,
	|	ТабельРабот.Номер,
	|	ТабельРабот.Организация,
	|	ТабельРабот.Контрагент,
	|	ТабельРабот.ВалютаВзаиморасчетов
	|ИЗ
	|	Документ.ТабельРабот КАК ТабельРабот
	|ГДЕ
	|	ТабельРабот.Ссылка В(&Ссылка)";
	
	Запрос.Параметры.Вставить("Ссылка", Ссылка);
	Шапка = Запрос.Выполнить().Выбрать();
	Шапка.Следующий();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РегламентноеЗадание.АналитическийПризнак,
	|	РегламентноеЗадание.ТемаСобытия,
	|	РегламентноеЗадание.Куратор.Наименование + "" (куратор)"" КАК Куратор,
	|	РегламентноеЗадание.Сотрудник.Наименование + "" (исполнитель)"" КАК Исполнитель,
	|	РегламентноеЗадание.Важность,
	|	РегламентноеЗадание.Состояние,
	|	ТабельРаботУслуги.КоличествоЧасовЗатрачено КАК КвоЧасовОбщее,
	|	ТабельРаботУслуги.Сумма КАК Сумма
	|ИЗ
	|	Документ.ТабельРабот.Услуги КАК ТабельРаботУслуги
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РегламентноеЗадание КАК РегламентноеЗадание
	|		ПО ТабельРаботУслуги.ДокументРасчета = РегламентноеЗадание.Ссылка
	|ГДЕ
	|	ТабельРаботУслуги.Ссылка В(&Ссылка)
	//|		И ТабельРаботУслуги.ДокументРасчета = ЗНАЧЕНИЕ()
	|ИТОГИ
	|	СУММА(КвоЧасовОбщее),
	|	СУММА(Сумма)
	|ПО
	|	ОБЩИЕ,
	|	Куратор,
	|	Исполнитель";
	Запрос.Параметры.Вставить("Ссылка", Ссылка);
	РезультатЗапроса = Запрос.Выполнить();

	
	//-------------- ВЫВОД печатной формы --------------------
	Макет = Документы.ТабельРабот.ПолучитьМакет("ПечатьТабеляКратко");
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьЗаголовок.Параметры.ТекстЗаголовка = ОбщегоНазначения.СформироватьЗаголовокДокумента(Шапка, Ссылка.Получить(0).Метаданные().Синоним);
	ТабДокумент.Вывести(ОбластьЗаголовок);
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьШапка.Параметры.Заполнить(Шапка);
	ТабДокумент.Вывести(ОбластьШапка);
	
	ТабДокумент.НачатьАвтогруппировкуСтрок();

	КвоСтрок = 0;
	СуммаКПрописи = 0;
	ОбходОбщиеИтоги = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ОбходОбщиеИтоги.Следующий() Цикл
		СуммаКПрописи = СуммаКПрописи + ОбходОбщиеИтоги.Сумма;
		ВыборкаКуратор = ОбходОбщиеИтоги.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаКуратор.Следующий() Цикл
			
			ОбластьКуратор = Макет.ПолучитьОбласть("Куратор");
			ОбластьКуратор.Параметры.Заполнить(ВыборкаКуратор);
			ТабДокумент.Вывести(ОбластьКуратор, ВыборкаКуратор.Уровень());
			
			ВыборкаИсполнитель = ВыборкаКуратор.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаИсполнитель.Следующий() Цикл
				
				ОбластьИсполнитель = Макет.ПолучитьОбласть("Исполнитель");
				ОбластьИсполнитель.Параметры.Заполнить(ВыборкаИсполнитель);
				ТабДокумент.Вывести(ОбластьИсполнитель, ВыборкаИсполнитель.Уровень());
				
				БылаПечатьШапкиТаблицы = Ложь;
				ВыборкаДетали = ВыборкаИсполнитель.Выбрать();
				Пока ВыборкаДетали.Следующий() Цикл
					
					КвоСтрок = КвоСтрок + 1;
					
					Если НЕ БылаПечатьШапкиТаблицы Тогда
						
						ОбластьШапкиТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
						ТабДокумент.Вывести(ОбластьШапкиТаблицы,ВыборкаДетали.Уровень(),,Истина);
						
						БылаПечатьШапкиТаблицы = Истина;
						
					КонецЕсли;	
					
					ОбластьДетальныхЗаписей = Макет.ПолучитьОбласть("Строка");
					ОбластьДетальныхЗаписей.Параметры.Заполнить(ВыборкаДетали);
					ТабДокумент.Вывести(ОбластьДетальныхЗаписей, ВыборкаДетали.Уровень(),,Ложь);
					
				КонецЦикла;	
				
			КонецЦикла;	
			
		КонецЦикла;	
		
		ОбластьВсего = Макет.ПолучитьОбласть("Всего");
		ОбластьВсего.Параметры.Заполнить(ОбходОбщиеИтоги);
		ТабДокумент.Вывести(ОбластьВсего, ОбходОбщиеИтоги.Уровень());

	КонецЦикла;
	ТабДокумент.ЗакончитьАвтогруппировкуСтрок();

	ОбластьИтоговаяСтрока = Макет.ПолучитьОбласть("ИтоговаяСтрока");
	ОбластьИтоговаяСтрока.Параметры.ИтоговаяСтрока =  "Всего наименований " + КвоСтрок 
									+ ", на сумму " + ОбщегоНазначения.ФорматСумм(СуммаКПрописи, Шапка.ВалютаВзаиморасчетов);
	ОбластьИтоговаяСтрока.Параметры.СуммаПрописью = ОбщегоНазначения.СформироватьСуммуПрописью(СуммаКПрописи, Шапка.ВалютаВзаиморасчетов);
	ТабДокумент.Вывести(ОбластьИтоговаяСтрока);
	
	
КонецПроцедуры

Функция ПолучитьПараметрыСекцииШапкаРасширено(ДокЗадание)
	
	Параметры = Новый Структура();
	
	ЗапросПодразделений = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОбъектыОбслуживанияПодразделения.Подразделение
	|ИЗ
	|	Документ.РегламентноеЗадание.ОбъектыОбслуживания КАК ОбъектыОбслуживанияПодразделения
	|ГДЕ
	|	ОбъектыОбслуживанияПодразделения.Ссылка = &Ссылка
	|	И ОбъектыОбслуживанияПодразделения.Пометка
	|	И (НЕ ОбъектыОбслуживанияПодразделения.Подразделение = ЗНАЧЕНИЕ(Справочник.Подразделения.ПустаяСсылка))");
	
	ЗапросПодразделений.УстановитьПараметр("Ссылка",ДокЗадание);
	ВыборкаПодразделений = ЗапросПодразделений.Выполнить().Выбрать();
	СтрПодразделений = "";
	
	номПрохода = 0;
	Пока ВыборкаПодразделений.Следующий() Цикл
		номПрохода = номПрохода +1;
		СтрПодразделений = СтрПодразделений + Строка(ВыборкаПодразделений.Подразделение)+?(ВыборкаПодразделений.Количество()>номПрохода,Символы.ПС,"");
	КонецЦикла;	
	
	ЗапросОбъектов = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОбъектыОбслуживанияОбъекты.ОбъектОбслуживания
	|ИЗ
	|	Документ.РегламентноеЗадание.ОбъектыОбслуживания КАК ОбъектыОбслуживанияОбъекты
	|ГДЕ
	|	ОбъектыОбслуживанияОбъекты.Ссылка = &Ссылка
	|	И ОбъектыОбслуживанияОбъекты.Пометка
	|	И (НЕ ОбъектыОбслуживанияОбъекты.ОбъектОбслуживания = ЗНАЧЕНИЕ(Справочник.ОбъектыОбслуживания.ПустаяСсылка))");
	
	ЗапросОбъектов.УстановитьПараметр("Ссылка",ДокЗадание);
	ВыборкаОбъектов = ЗапросОбъектов.Выполнить().Выбрать();
	СтрОбъектов = "";
	
	номПрохода = 0;
	Пока ВыборкаОбъектов.Следующий() Цикл
		номПрохода = номПрохода +1;
		СтрОбъектов = СтрОбъектов + Строка(ВыборкаОбъектов.ОбъектОбслуживания)+?(ВыборкаОбъектов.Количество()>номПрохода,Символы.ПС,"");
	КонецЦикла;	
	
	//ЗапросКонтакты = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ
	//|	ОбъектыОбслуживанияКонтакты.КонтактноеЛицо
	//|ИЗ
	//|	Документ.РегламентноеЗадание.ОбъектыОбслуживания КАК ОбъектыОбслуживанияКонтакты
	//|ГДЕ
	//|	ОбъектыОбслуживанияКонтакты.Ссылка = &Ссылка
	//|	И ОбъектыОбслуживанияКонтакты.Пометка
	//|	И (НЕ ОбъектыОбслуживанияКонтакты.КонтактноеЛицо = ЗНАЧЕНИЕ(Справочник.КонтактныеЛицаКонтрагентов.ПустаяСсылка))");
	//
	//ЗапросКонтакты.УстановитьПараметр("Ссылка",ДокЗадание);
	//ВыборкаКонтактов = ЗапросКонтакты.Выполнить().Выбрать();
	//СтрКонтакты = "";
	//
	//номПрохода = 0;
	//Пока ВыборкаКонтактов.Следующий() Цикл
	//	номПрохода = номПрохода +1;
	//	СтрКонтакты = СтрКонтакты + Строка(ВыборкаКонтактов.КонтактноеЛицо)+?(ВыборкаКонтактов.Количество()>номПрохода,Символы.ПС,"");
	//КонецЦикла;	

	Параметры.Вставить("ПредставлениеПодразделений",СтрПодразделений);
	Параметры.Вставить("ОбъектыОбслуживания",СтрОбъектов);
	Параметры.Вставить("КонтактныеЛица",ДокЗадание.КонтактноеЛицоКонтрагента);
	
	Возврат Параметры;
КонецФункции //ПолучитьПараметрыСекцииШапкаРасширено()	

Функция ПолучитьПараметрыСекцииСтрокаРасширено(ДокЗадание,РасчетнаяСумма)
	
	Параметры = Новый ТаблицаЗначений;
	Параметры.Колонки.Добавить("НомерСтроки");
	Параметры.Колонки.Добавить("ПечРешений");
	Параметры.Колонки.Добавить("КвоЧасовОбщее");
	Параметры.Колонки.Добавить("ДатаИсполнения");
	Параметры.Колонки.Добавить("Сумма");
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ТиповыеРешения.НомерСтроки,
	|	ТиповыеРешения.ТиповоеРешение.Наименование КАК Заголовок,
	|	ТиповыеРешения.ПодробноеОписание,
	|	ТиповыеРешения.КоличествоЧасовЗатрачено,
	|	ТиповыеРешения.ДатаИсполнения КАК ДатаИсполнения
	|ИЗ
	|	Документ.РегламентноеЗадание.ТиповыеРешения КАК ТиповыеРешения
	|ГДЕ
	|	ТиповыеРешения.Ссылка = &Ссылка
	|	И ТиповыеРешения.КоличествоЧасовЗатрачено > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаИсполнения");
	
	Запрос.УстановитьПараметр("Ссылка",ДокЗадание);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Параметры.Добавить();
		НоваяСтрока.НомерСтроки = Выборка.НомерСтроки;
		НоваяСтрока.ПечРешений  = Строка(Выборка.Заголовок) + Символы.ПС + Строка(СокрЛП(Выборка.ПодробноеОписание));
		НоваяСтрока.КвоЧасовОбщее = Выборка.КоличествоЧасовЗатрачено;
		НоваяСтрока.ДатаИсполнения = Выборка.ДатаИсполнения;
		НоваяСтрока.Сумма = РасчетнаяСумма*Выборка.КоличествоЧасовЗатрачено;
	КонецЦикла;	

	Возврат Параметры;
КонецФункции	

Процедура ПечатьТабеляПодробно(ТабДокумент, Ссылка) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТабельРабот.Дата,
	|	ТабельРабот.Номер,
	|	ТабельРабот.Организация,
	|	ТабельРабот.Контрагент,
	|	ТабельРабот.ВалютаВзаиморасчетов
	|ИЗ
	|	Документ.ТабельРабот КАК ТабельРабот
	|ГДЕ
	|	ТабельРабот.Ссылка В(&Ссылка)";
	
	Запрос.Параметры.Вставить("Ссылка", Ссылка);
	Шапка = Запрос.Выполнить().Выбрать();
	Шапка.Следующий();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РегламентноеЗадание.Ссылка КАК ДокСсылка,
	|	РегламентноеЗадание.АналитическийПризнак,
	|	РегламентноеЗадание.ТемаСобытия,
	|	РегламентноеЗадание.Куратор.Наименование + "" (куратор)"" КАК Куратор,
	|	РегламентноеЗадание.Сотрудник.Наименование + "" (исполнитель)"" КАК Исполнитель,
	|	РегламентноеЗадание.Важность,
	|	РегламентноеЗадание.Состояние,
	|	ТабельРаботУслуги.КоличествоЧасовЗатрачено КАК КвоЧасовОбщее,
	|	ТабельРаботУслуги.Сумма КАК Сумма,
	|	РегламентноеЗадание.Дата,
	|	РегламентноеЗадание.ДатаСдачиРабот,
	|	РегламентноеЗадание.ОписаниеЗадания,
	|	РегламентноеЗадание.ОписаниеРешения
	|ИЗ
	|	Документ.ТабельРабот.Услуги КАК ТабельРаботУслуги
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РегламентноеЗадание КАК РегламентноеЗадание
	|		ПО ТабельРаботУслуги.ДокументРасчета = РегламентноеЗадание.Ссылка
	|ГДЕ
	|	ТабельРаботУслуги.Ссылка В(&Ссылка)
	|ИТОГИ
	|	СУММА(КвоЧасовОбщее),
	|	СУММА(Сумма)
	|ПО
	|	ОБЩИЕ,
	|	Куратор,
	|	Исполнитель";
	Запрос.Параметры.Вставить("Ссылка", Ссылка);
	РезультатЗапроса = Запрос.Выполнить();

	
	//-------------- ВЫВОД печатной формы --------------------
	Макет = Документы.ТабельРабот.ПолучитьМакет("ПечатьТабеляПодробно");
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьЗаголовок.Параметры.ТекстЗаголовка = ОбщегоНазначения.СформироватьЗаголовокДокумента(Шапка, Ссылка.Получить(0).Метаданные().Синоним);
	ТабДокумент.Вывести(ОбластьЗаголовок);
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьШапка.Параметры.Заполнить(Шапка);
	ТабДокумент.Вывести(ОбластьШапка);
	
	ТабДокумент.НачатьАвтогруппировкуСтрок();

	КвоСтрок = 0;
	СуммаКПрописи = 0;
	ОбходОбщиеИтоги = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ОбходОбщиеИтоги.Следующий() Цикл
		СуммаКПрописи = СуммаКПрописи + ОбходОбщиеИтоги.Сумма;
		ВыборкаКуратор = ОбходОбщиеИтоги.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаКуратор.Следующий() Цикл
			
			ОбластьКуратор = Макет.ПолучитьОбласть("Куратор");
			ОбластьКуратор.Параметры.Заполнить(ВыборкаКуратор);
			ТабДокумент.Вывести(ОбластьКуратор, ВыборкаКуратор.Уровень());
			
			ВыборкаИсполнитель = ВыборкаКуратор.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаИсполнитель.Следующий() Цикл
				
				ОбластьИсполнитель = Макет.ПолучитьОбласть("Исполнитель");
				ОбластьИсполнитель.Параметры.Заполнить(ВыборкаИсполнитель);
				ТабДокумент.Вывести(ОбластьИсполнитель, ВыборкаИсполнитель.Уровень());
				
				БылаПечатьШапкиТаблицы = Ложь;
				ВыборкаДетали = ВыборкаИсполнитель.Выбрать();
				Пока ВыборкаДетали.Следующий() Цикл
					
					КвоСтрок = КвоСтрок + 1;
					
					Если НЕ БылаПечатьШапкиТаблицы Тогда
						
						ОбластьШапкиТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
						ТабДокумент.Вывести(ОбластьШапкиТаблицы,ВыборкаДетали.Уровень(),,Истина);
						
						БылаПечатьШапкиТаблицы = Истина;
						
					КонецЕсли;	
					
					ОбластьДетальныхЗаписей = Макет.ПолучитьОбласть("Строка");
					ОбластьДетальныхЗаписей.Параметры.Заполнить(ВыборкаДетали);
					ТабДокумент.Вывести(ОбластьДетальныхЗаписей, ВыборкаДетали.Уровень(),,Ложь);
					
					//более детальная аналитика расположена по подразделениям и объектам обслуживания
					//а также этапы решения программиста
					ПараметрыШапки = ПолучитьПараметрыСекцииШапкаРасширено(ВыборкаДетали.ДокСсылка);
					ОбластьРасширеннойШапки = Макет.ПолучитьОбласть("ШапкаРасширено");
					ОбластьРасширеннойШапки.Параметры.Заполнить(ПараметрыШапки);
					ТабДокумент.Вывести(ОбластьРасширеннойШапки, ВыборкаДетали.Уровень()+1,,Ложь);
					
					//разрез по решениям
					ПараметрыРешений = ПолучитьПараметрыСекцииСтрокаРасширено(ВыборкаДетали.ДокСсылка,ВыборкаДетали.Сумма/ВыборкаДетали.КвоЧасовОбщее);
					ОбластьРасширеннойСтроки = Макет.ПолучитьОбласть("СтрокаРасширено");
					Для Каждого СтрРешений Из ПараметрыРешений Цикл
						ОбластьРасширеннойСтроки.Параметры.Заполнить(СтрРешений);
						ТабДокумент.Вывести(ОбластьРасширеннойСтроки, ВыборкаДетали.Уровень()+1,,Ложь);
					КонецЦикла;
					
				КонецЦикла;	
				
			КонецЦикла;	
			
		КонецЦикла;	
		
		ОбластьВсего = Макет.ПолучитьОбласть("Всего");
		ОбластьВсего.Параметры.Заполнить(ОбходОбщиеИтоги);
		ТабДокумент.Вывести(ОбластьВсего, ОбходОбщиеИтоги.Уровень());

	КонецЦикла;
	ТабДокумент.ЗакончитьАвтогруппировкуСтрок();

	ОбластьИтоговаяСтрока = Макет.ПолучитьОбласть("ИтоговаяСтрока");
	ОбластьИтоговаяСтрока.Параметры.ИтоговаяСтрока =  "Всего наименований " + КвоСтрок 
									+ ", на сумму " + ОбщегоНазначения.ФорматСумм(СуммаКПрописи, Шапка.ВалютаВзаиморасчетов);
	ОбластьИтоговаяСтрока.Параметры.СуммаПрописью = ОбщегоНазначения.СформироватьСуммуПрописью(СуммаКПрописи, Шапка.ВалютаВзаиморасчетов);
	ТабДокумент.Вывести(ОбластьИтоговаяСтрока);
	
	
КонецПроцедуры

Процедура ПечатьТабеляДляКлиента(ТабДокумент, Ссылка) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ТабельРаботУслуги.ДокументРасчета,
	                      |	ТабельРаботУслуги.КоличествоЧасовЗатрачено,
	                      |	ТабельРаботУслуги.Сумма,
	                      |	ТабельРаботУслуги.ЦенаЧаса
	                      |ИЗ
	                      |	Документ.ТабельРабот.Услуги КАК ТабельРаботУслуги
	                      |ГДЕ
	                      |	ТабельРаботУслуги.Ссылка = &Ссылка");
	
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	
	//-------------- ВЫВОД печатной формы --------------------
	Макет = Документы.ТабельРабот.ПолучитьМакет("ПечатьДляКлиента");
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьЗаголовок.Параметры.ТекстЗаголовка = ОбщегоНазначения.СформироватьЗаголовокДокумента(Ссылка, Ссылка.Метаданные().Синоним);
	ТабДокумент.Вывести(ОбластьЗаголовок);

	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьШапка.Параметры.Заполнить(Ссылка);
	ТабДокумент.Вывести(ОбластьШапка);

	ОбластьШапкиТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ТабДокумент.Вывести(ОбластьШапкиТаблицы);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();

	КвоСтрок = 0;
	Пока Выборка.Следующий() Цикл
		
		КвоСтрок = КвоСтрок + 1;
		ОбластьСтроки = Макет.ПолучитьОбласть("Строка");
		
		ТЧ_ЭтапыРешений = Выборка.ДокументРасчета.ТиповыеРешения;
		ПечЗадание 										= "Тема: "+Выборка.ДокументРасчета.ТемаСобытия + Символы.ПС + Выборка.ДокументРасчета.ОписаниеЗадания;
		ОбластьСтроки.Параметры.ПечЗадание 				= ПечЗадание;
		ОбластьСтроки.Параметры.Дата 					= Выборка.ДокументРасчета.Дата;
		ОбластьСтроки.Параметры.ДатаСдачиРабот 			= Выборка.ДокументРасчета.ДатаСдачиРабот;
		ОбластьСтроки.Параметры.АналитическийПризнак 	= Выборка.ДокументРасчета.АналитическийПризнак;
		ОбластьСтроки.Параметры.КонтактноеЛицо 			= Выборка.ДокументРасчета.КонтактноеЛицоКонтрагента;
		ОбластьСтроки.Параметры.КвоЧасовОбщее 			= Выборка.КоличествоЧасовЗатрачено;
		ОбластьСтроки.Параметры.Сумма 					= Выборка.Сумма;
		
		ПечРешение = "";
		Для Каждого СтрокаРешения Из ТЧ_ЭтапыРешений Цикл
			ПечРешение = ПечРешение + СокрЛП(СтрокаРешения.ПодробноеОписание) + Символы.ПС;
		КонецЦикла;	
		
		ОбластьСтроки.Параметры.ПечРешение = ПечРешение;
		ТабДокумент.Вывести(ОбластьСтроки);
	КонецЦикла;	
	
	ОбластьВсего = Макет.ПолучитьОбласть("Всего");
	ОбластьВсего.Параметры.КвоЧасовОбщее = Ссылка.Услуги.Итог("КоличествоЧасовЗатрачено");
	ОбластьВсего.Параметры.Сумма = Ссылка.Услуги.Итог("Сумма");
	ТабДокумент.Вывести(ОбластьВсего);
	
	СуммаКПрописи = Ссылка.Услуги.Итог("Сумма");
	ОбластьИтоговаяСтрока = Макет.ПолучитьОбласть("ИтоговаяСтрока");
	ОбластьИтоговаяСтрока.Параметры.ИтоговаяСтрока =  "Всего наименований " + КвоСтрок 
									+ ", на сумму " + ОбщегоНазначения.ФорматСумм(СуммаКПрописи, Ссылка.ВалютаВзаиморасчетов);
	ОбластьИтоговаяСтрока.Параметры.СуммаПрописью = ОбщегоНазначения.СформироватьСуммуПрописью(СуммаКПрописи, Ссылка.ВалютаВзаиморасчетов);
	ТабДокумент.Вывести(ОбластьИтоговаяСтрока);
	
КонецПроцедуры	