&НаСервере
Процедура ЗаполнитьДеревоПрав(Дерево)
	
	Дерево.Строки.Очистить();
	
	Если НЕ ЗначениеЗаполнено(Запись.Пользователь) Тогда
		Модифицированность = Ложь;
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	Права.Родитель,
	|	Права.Ссылка,
	|	Права.ЭтоГруппа,
	|	ЗначениеПрав.Значение,
	|	ВЫБОР
	|		КОГДА Права.ЭтоГруппа
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК НомерКартинки
	|ИЗ
	|	ПланВидовХарактеристик.ПраваПользователей КАК Права
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияДополнительныхПравПользователя КАК ЗначениеПрав
	|		ПО (ЗначениеПрав.Право = Права.Ссылка)
	|			И (ЗначениеПрав.Пользователь = &Пользователь)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Права.ЭтоГруппа ИЕРАРХИЯ,
	|	Права.Наименование");
	Запрос.УстановитьПараметр("Пользователь", Запись.Пользователь);
	Запрос.Выполнить();
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если  Не Выборка.Родитель.Пустая() Тогда
			СтрокаГруппы = Дерево.Строки.Найти(Выборка.Родитель, "Право", Истина);
			Если СтрокаГруппы=Неопределено Тогда
				СтрокаГруппы = Дерево.Строки.Добавить();
				СтрокаГруппы.Право = Выборка.Родитель;
				СтрокаГруппы.ЭтоГруппа = Выборка.ЭтоГруппа;
				СтрокаГруппы.НомерКартинки = Выборка.НомерКартинки;
			КонецЕсли;
		Иначе
			СтрокаГруппы = Дерево;
		КонецЕсли;		
		СтрокаПрава = СтрокаГруппы.Строки.Добавить();
		СтрокаПрава.Право = Выборка.Ссылка;
		СтрокаПрава.Значение = Выборка.Ссылка.ТипЗначения.ПривестиЗначение(Выборка.Значение);
		СтрокаПрава.ЭтоГруппа = Выборка.ЭтоГруппа;
		СтрокаПрава.НомерКартинки = Выборка.НомерКартинки;
	КонецЦикла;
	
	Модифицированность = Ложь;

КонецПроцедуры	

&НаСервере
Процедура ЗаполнитьДеревоЗначенийСвойств()
	
	ДеревоКакОбъект = ДанныеФормыВЗначение(ДеревоПрав,Тип("ДеревоЗначений"));
	ЗаполнитьДеревоПрав(ДеревоКакОбъект);
	ЭтаФорма.ЗначениеВРеквизитФормы(ДеревоКакОбъект, "ДеревоПрав");
	
КонецПроцедуры


&НаСервере
Процедура ОпределитьДоступностьФормы()
	
	Если Не РольДоступна("ПолныеПрава") Тогда		
		ТолькоПросмотр = Истина;
	КонецЕсли;

КонецПроцедуры	

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Запись.Пользователь = ОбщегоНазначения.ПолучитьЗначениеПеременной("глТекущийПользователь");
	ОпределитьДоступностьФормы();
	ЗаполнитьДеревоЗначенийСвойств();
	УправлениеСвойствамиКлиент.РазвернутьДеревоЗначенийСвойств(Элементы.ДеревоПрав,ДеревоПрав);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьНастройки()
	
	Если НЕ ЗначениеЗаполнено(Запись.Пользователь) Тогда
		Возврат;
	КонецЕсли;
	
	Набор = РегистрыСведений.ЗначенияДополнительныхПравПользователя.СоздатьНаборЗаписей();
	Набор.Отбор.Пользователь.Использование = Истина;
	Набор.Отбор.Пользователь.Значение = Запись.Пользователь;
	ДеревоКакОбъект = ДанныеФормыВЗначение(ДеревоПрав,Тип("ДеревоЗначений"));
	ЗаполнитьНаборЗаписей(ДеревоКакОбъект.Строки, Набор);
	Набор.Записать();
	ЭтаФорма.Модифицированность = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаборЗаписей(СтрокиДерева, НаборЗаписей)
	Для Каждого СтрокаДерева Из СтрокиДерева Цикл
		Если Не СтрокаДерева.ЭтоГруппа Тогда
			ЗаписьОбъекта = НаборЗаписей.Добавить();
			ЗаписьОбъекта.Пользователь = Запись.Пользователь;
			ЗаписьОбъекта.Право = СтрокаДерева.Право;
			ЗаписьОбъекта.Значение = СтрокаДерева.Право.ТипЗначения.ПривестиЗначение(СтрокаДерева.Значение);
		Иначе
			ЗаполнитьНаборЗаписей(СтрокаДерева.Строки, НаборЗаписей);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры


&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ОбновитьНастройки();
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьЭлемент(Команда)
	
	ОбновитьНастройки();
	
КонецПроцедуры


&НаКлиенте
Процедура ПользовательПриИзменении(Элемент)
	
	ЗаполнитьДеревоЗначенийСвойств();
	УправлениеСвойствамиКлиент.РазвернутьДеревоЗначенийСвойств(Элементы.ДеревоПрав,ДеревоПрав);
	
КонецПроцедуры


&НаКлиенте
Процедура ЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	м =  Элемент;
	
КонецПроцедуры

